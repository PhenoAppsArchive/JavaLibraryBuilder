apply plugin: "java-library"

ext {
    gson_version                = "2.8.6"
    okhttp_version              = "2.7.5"
    gsonfire_version            = "1.8.3"
    swagger_annotations_version = "2.0.10"
    javax_annotation_version    = "1.3.2"
    threetenbp_version          = "1.4.0"
    junit_version               = "4.13"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    api "com.google.code.gson:gson:${gson_version}"
    api "com.squareup.okhttp:okhttp:${okhttp_version}"
    api "com.squareup.okhttp:logging-interceptor:${okhttp_version}"
    api "io.gsonfire:gson-fire:${gsonfire_version}"
    api "io.swagger.core.v3:swagger-annotations:${swagger_annotations_version}"
    api "javax.annotation:javax.annotation-api:${javax_annotation_version}"
    api "org.threeten:threetenbp:${threetenbp_version}"

    implementation "junit:junit:${junit_version}"
}

sourceCompatibility = "7"
targetCompatibility = "7"

////////////////////////////////////////////////////////////////////////////////////////////////////

apply plugin: "maven-publish"

publishing.publications.all {
    pom.withXml {
        asNode().dependencies."*".findAll() {
            it.scope.text() == "runtime" && project.configurations.compile.allDependencies.find { dep ->
                dep.name == it.artifactId.text()
            }
        }.each { it.scope*.value = "compile"}
    }
}

ext {
    bintrayGroupId    = "org.phenoapps"
    bintrayArtifactId = "javalib"
    bintrayVersion    = "1.0.2"
    url               = "https://github.com/PhenoLibs/JavaLibraryBuilder"
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from       components.java
            groupId    "${bintrayGroupId}"
            artifactId "${bintrayArtifactId}"
            version    "${bintrayVersion}"
            pom {
                name        = "${bintrayArtifactId}"
                description = "${bintrayArtifactId}"
                url         = "${url}"
                licenses {
                    license {
                        name = "GNU General Public License version 2"
                        url  = "https://opensource.org/licenses/gpl-2.0.php"
                    }
                }
                scm { url = "${url}" }
            }
        }
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////

// USAGE:
// Run the command
// 'export JAVA_HOME="/Applications/Android Studio.app/Contents/jre/jdk/Contents/Home"' if you
// haven't already.
//
// Run the command './gradlew :javalib:bintrayUpload'

apply plugin: "com.jfrog.bintray"

Properties properties = new Properties()
properties.load(project.rootProject.file("secrets.properties").newDataInputStream())

bintray {
    user         = properties.getProperty("bintray.username")
    key          = properties.getProperty("bintray.apiKey"  )
    publications = ["myPublication"]
    pkg {
        repo     = "pheno-apps-java-client-library"
        name     = "${bintrayGroupId}:${bintrayArtifactId}"
        licenses = ["GPL-2.0"]
        vcsUrl   = "${url}"

        userOrg = "pheno-apps"
        version {
            name     = "${bintrayVersion}"
            released = new Date()
        }
    }
}